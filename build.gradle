plugins {
    id "java"
    id "eclipse"
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.8"
}

allprojects {
    group = "dev.airyy.AiryLib"
    version = "0.0.1"
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    group = "dev.airyy.AiryLib"
    version = "0.0.1"

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }

    dependencies {
        implementation 'org.jetbrains:annotations:26.0.2'
    }

    def targetJavaVersion = 21
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"

        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release.set(targetJavaVersion)
        }
    }

    publishing {
        repositories {
            maven {
                url = uri("https://maven.airymc.net/releases")

                credentials {
                    username = findProperty("reposiliteUsername") ?: ""
                    password = findProperty("reposilitePassword") ?: ""
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = project.name
            }
        }
    }
}